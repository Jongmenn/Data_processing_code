LIBNAME A 'D:\EUMC\데이터관리\표본코호트_DB(NHIS_NSC)\JK';
LIBNAME T20 'D:\EUMC\데이터관리\표본코호트_DB(NHIS_NSC)\T120';
PROC FREQ DATA=A.JK; TABLES CTRB_PT_TYPE_CD; RUN;

DATA A.MEDI; 
RETAIN KEY;
SET A.JK; IF CTRB_PT_TYPE_CD IN (0); 
PKEY=COMPRESS(STND_Y)||("-")||COMPRESS(PERSON_ID);
DROP DFAB_GRD_CD DFAB_PTN_CD DFAB_REG_YM IPSN_TYPE_CD;
RUN;

PROC SORT DATA=A.MEDI; BY PERSON_ID STND_Y; RUN;

PROC SQL; CREATE TABLE T20.T20_MEDI AS SELECT * FROM T20.T120 WHERE PERSON_ID IN (SELECT PERSON_ID FROM A.MEDI); QUIT;

DATA T20.T20_MEDI_REV; SET T20.T20_MEDI;
PKEY=COMPRESS(SUBSTR(RECU_FR_DT,1,4))||("-")||COMPRESS(PERSON_ID);
KEEP PKEY DMD_TRAMT; RUN;

PROC SQL; CREATE TABLE T20.T20_MEDI_TOT_PAY AS SELECT PKEY, SUM(DMD_TRAMT) AS TOT_PAY FROM T20.T20_MEDI_REV GROUP BY PKEY; QUIT;

PROC SQL; CREATE TABLE A.MEDI_PAY AS SELECT * FROM A.MEDI AS A LEFT JOIN T20.T20_MEDI_TOT_PAY AS B ON A.PKEY=B.PKEY; QUIT;

/*그림예시, 성별에 따른 연도별 총 진료비 차이가 있는지?*/
PROC SGPLOT DATA=A.MEDI_PAY; 
VLINE STND_Y/RESPONSE=TOT_PAY GROUP=SEX STAT=MEAN LIMITSTAT=STDERR;
YAXIS LABEL="MEAN+/-SE"; RUN;

DATA A.MEDI_PAY2; SET A.MEDI_PAY; 
IF STND_Y IN (2010,2011,2012,2013);
IF SIDO IN (11);
LN_TOT_PAY=LOG(TOT_PAY); 
RUN;

DATA A.MEDI_PAY3; SET A.MEDI_PAY2; IF PERSON_ID IN (10000034, 10000130,10000220,10000803,10000907,10000949,10001217,10001939,12318560); RUN;

PROC UNIVARIATE DATA=A.MEDI_PAY2 NORMAL PLOT; 
VAR  TOT_PAY;
HISTOGRAM  TOT_PAY /NORMAL (MU=EST SIGMA=EST); RUN;

PROC UNIVARIATE DATA=A.MEDI_PAY2 NORMAL PLOT; 
VAR  LN_TOT_PAY;
HISTOGRAM  LN_TOT_PAY /NORMAL (MU=EST SIGMA=EST); RUN;

/*단일 반복요인 자료 */
PROC GLM DATA=A.MEDI_PAY2;
CLASS PERSON_ID SEX(REF="1") AGE_GROUP STND_Y;
MODEL LN_TOT_PAY = SEX AGE_GROUP STND_Y /SOLUTION; /*계수포함*/
RANDOM PERSON_ID;
RUN;

/*GLMM (RANDOM EFFECT)*/
PROC MIXED DATA=A.MEDI_PAY2 METHOD=REML COVTEST;
CLASS PERSON_ID SEX(REF="1") AGE_GROUP SIDO STND_Y;
MODEL LN_TOT_PAY = SEX AGE_GROUP SIDO STND_Y /SOLUTION; /*계수포함*/
RANDOM INTERCEPT/SUBJECT=PERSON_ID;
/*LSMEANS SEX AGE_GROUP  SIDO/DIFF  CL;*/
RUN;

/*Repeated model*/
PROC MIXED DATA=A.MEDI_PAY2 METHOD=REML COVTEST;
CLASS PERSON_ID SEX(REF="1") AGE_GROUP SIDO;
MODEL LN_TOT_PAY = SEX AGE_GROUP SIDO STND_Y /SOLUTION; /*계수포함*/
RANDOM INTERCEPT/SUBJECT=PERSON_ID type=cs;
/*LSMEANS SEX AGE_GROUP  SIDO/DIFF  CL;*/
RUN;

/*PROC GLIMMIX (반응변수 연속이니 유사)*/
PROC GLiMMIX DATA=A.MEDI_PAY2 NOCLPRINT PLOTS=ALL;
CLASS PERSON_ID SEX(REF="1") AGE_GROUP SIDO STND_Y;
MODEL LN_TOT_PAY = SEX AGE_GROUP SIDO STND_Y /DIST=NORMAL solution; 
RANDOM INTERCEPT/SUB=PERSON_ID ; RUN;

/*PROC PANAL*/
PROC SORT DATA=A.MEDI_PAY2; BY PERSON_ID STND_Y; QUIT;

DATA A.MEDI_PAY3; SET A.MEDI_PAY2; IF PERSON_ID ^=".";RUN;

PROC PANEL DATA=A.MEDI_PAY3; 
ID PERSON_ID STND_Y;
CLASS PERSON_ID SEX AGE_GROUP SIDO STND_Y ;
MODEL LN_TOT_PAY=SEX AGE_GROUP SIDO STND_Y /FIXONE;
RUN;
